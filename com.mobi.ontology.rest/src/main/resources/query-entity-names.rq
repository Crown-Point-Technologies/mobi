PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skos-xl: <http://www.w3.org/2008/05/skos-xl#>

SELECT
    ?entity
    (SAMPLE(?finalPrefNames) as ?prefName)
    (GROUP_CONCAT(DISTINCT ?label;separator="�") as ?rdfs_label)
    (GROUP_CONCAT(DISTINCT ?dctTitle;separator="�") as ?dcterms_title)
    (GROUP_CONCAT(DISTINCT ?dceTitle;separator="�") as ?dc_title)
    (GROUP_CONCAT(DISTINCT ?prefLabel;separator="�") as ?skos_prefLabel)
    (GROUP_CONCAT(DISTINCT ?altLabel;separator="�") as ?skos_altLabel)
    (GROUP_CONCAT(DISTINCT ?skosXLPrefLiteralForm;separator="�") as ?skosxl_prefLabel)
    (GROUP_CONCAT(DISTINCT ?skosXLAltLiteralForm;separator="�") as ?skosxl_altLabel)
WHERE {
    {
        SELECT
            ?entity
            ?prefNames
            ?label
            ?dctTitle
            ?dceTitle
            ?prefLabel
            ?altLabel
            ?skosXLPrefLiteralForm
            ?skosXLAltLiteralForm
        WHERE {
            ?entity a ?type .
            FILTER(isIRI(?entity))

            OPTIONAL {
                ?entity rdfs:label ?label .
            }
            OPTIONAL {
                ?entity dcterms:title ?dctTitle .
            }
            OPTIONAL {
                ?entity dc:title ?dceTitle .
            }
            OPTIONAL {
                ?entity skos:prefLabel ?prefLabel .
            }
            OPTIONAL {
                ?entity skos:altLabel ?altLabel .
            }
            OPTIONAL {
                ?entity skos-xl:prefLabel ?skosPrefLabel .
                ?skosPrefLabel a skos-xl:Label ;
                    skos-xl:literalForm ?skosXLPrefLiteralForm .
            }
            OPTIONAL {
                ?entity skos-xl:altLabel ?skosAltLabel .
                ?skosAltLabel a skos-xl:Label ;
                    skos-xl:literalForm ?skosXLAltLiteralForm .
            }

            BIND(COALESCE(
                ?label,
                ?dctTitle,
                ?dceTitle,
                ?prefLabel,
                ?altLabel,
                ?skosXLPrefLiteralForm,
                ?skosXLAltLiteralForm) as ?prefNames)

            FILTER(BOUND(?prefNames))
        }
    }

    OPTIONAL {
        FILTER(LANGMATCHES(LANG(?prefNames), "EN"))
        BIND(?prefNames as ?enPrefNames)
    }

    BIND(COALESCE(?enPrefNames, ?prefNames) as ?finalPrefNames)
} GROUP BY ?entity
