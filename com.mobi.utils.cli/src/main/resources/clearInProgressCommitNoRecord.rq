PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX catalog: <http://mobi.com/ontologies/catalog#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX usrmgmt: <http://mobi.com/ontologies/user/management#>

DELETE {
  GRAPH ?inProgressCommit {
    ?inProgressCommit ?p ?o .

    ?revision ?p2 ?o2 .
  }
  
  GRAPH ?diffGraph {
    ?s3 ?p3 ?o3 .
  }
}
WHERE {
  GRAPH ?inProgressCommit {
    ?inProgressCommit a catalog:InProgressCommit;
      catalog:onVersionedRDFRecord ?record ;
      prov:wasAssociatedWith ?user ;
      prov:generated ?revision ;
      ?p ?o .
        
    ?revision (catalog:additions | catalog:deletions) ?diffGraph;
      ?p2 ?o2 .
  }
  
  GRAPH ?diffGraph {
    ?s3 ?p3 ?o3 .
  }

  FILTER NOT EXISTS {?record a catalog:Record}
}
