PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX setting: <http://mobi.com/ontologies/setting#>
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {
    ?settingDef ?p ?o .
    ?attached ?attachedP ?attachedO .
    ?bnode ?bnodeP ?bnodeO .
}
WHERE {
    ?settingDef a sh:NodeShape ;
        setting:inGroup ?group ;
        rdfs:subClassOf ?settingType ;
        sh:property|sh:property/sh:node|sh:property/sh:node/sh:property ?attached ;
        ?p ?o .

    ?attached ?attachedP ?attachedO .

    OPTIONAL {
        ?attached (rdf:temp|!rdf:temp)* ?bnode .
        ?bnode ?bnodeP ?bnodeO .
        FILTER(isBlank(?bnode))
    }
}