PREFIX dc: <http://purl.org/dc/terms/>
PREFIX mcat: <http://matonto.org/ontologies/catalog#>

SELECT
    ?resource
    (GROUP_CONCAT(DISTINCT ?type ; separator = ",") AS ?types)
    ?title
    ?issued
    ?modified
    ?description
    ?identifier
    (GROUP_CONCAT(DISTINCT ?keyword ; separator = ",") AS ?keywords)
WHERE {
	?resource a ?type ;
		dc:title ?title ;
		dc:issued ?issued ;
		dc:modified ?modified .

	FILTER(?type IN (mcat:Record, mcat:UnversionedRecord, mcat:VersionedRecord, mcat:VersionedRDFRecord, mcat:OntologyRecord, mcat:MappingRecord, mcat:DatasetRecord))
	
	OPTIONAL { ?resource dc:description ?description } .
	OPTIONAL { ?resource dc:identifier ?identifier } .
    OPTIONAL { ?resource mcat:keyword ?keyword } .
} GROUP BY ?resource ?title ?issued ?modified ?description ?identifier
		