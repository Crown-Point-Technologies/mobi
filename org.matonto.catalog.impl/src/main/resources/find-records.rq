PREFIX dc: <http://purl.org/dc/terms/>
PREFIX mcat: <http://matonto.org/ontologies/catalog#>

SELECT
    ?record
    (GROUP_CONCAT(DISTINCT ?type ; separator = ",") AS ?types)
    ?title
    ?issued
    ?modified
    ?description
    ?identifier
    ?publisher
    (GROUP_CONCAT(DISTINCT ?keyword ; separator = ",") AS ?keywords)
WHERE {
	?record a ?type ;
	    mcat:catalog ?catalog ;
		dc:title ?title ;
		dc:issued ?issued ;
		dc:modified ?modified ;
		dc:publisher ?publisher ;
		?p ?o .

	FILTER(?type IN (mcat:Record, mcat:UnversionedRecord, mcat:VersionedRecord, mcat:VersionedRDFRecord, mcat:OntologyRecord, mcat:MappingRecord, mcat:DatasetRecord))

    FILTER EXISTS { ?record a ?type_filter . }

    FILTER IF(bound(?search_text), CONTAINS(lcase(?o), lcase(?search_text)), true)

	OPTIONAL { ?record dc:description ?description } .
    OPTIONAL { ?record mcat:keyword ?keyword } .
    OPTIONAL { ?record dc:identifier ?identifier } .
} GROUP BY ?record ?title ?issued ?modified ?description ?identifier ?publisher
